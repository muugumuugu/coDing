// by Etienne JACOB
// motion blur template by beesandbombs
// needs opensimplexnoise code in another tab
// --> code here : https://gist.github.com/Bleuje/fce86ef35b66c4a2b6a469b27163591e

int[][] result;
float t, c;

float ease(float p) {
  return 3*p*p - 2*p*p*p;
}

float ease(float p, float g) {
  if (p < 0.5) 
    return 0.5 * pow(2*p, g);
  else
    return 1 - 0.5 * pow(2*(1 - p), g);
}

float mn = .5*sqrt(3), ia = atan(sqrt(.5));

void push() {
  pushMatrix();
  pushStyle();
}

void pop() {
  popStyle();
  popMatrix();
}

void draw() {

  if (!recording) {
    t = mouseX*1.0/width;
    c = mouseY*1.0/height;
    if (mousePressed)
      println(c);
    draw_();
  } else {
    for (int i=0; i<width*height; i++)
      for (int a=0; a<3; a++)
        result[i][a] = 0;

    c = 0;
    for (int sa=0; sa<samplesPerFrame; sa++) {
      t = map(frameCount-1 + sa*shutterAngle/samplesPerFrame, 0, numFrames, 0, 1);
      draw_();
      loadPixels();
      for (int i=0; i<pixels.length; i++) {
        result[i][0] += pixels[i] >> 16 & 0xff;
        result[i][1] += pixels[i] >> 8 & 0xff;
        result[i][2] += pixels[i] & 0xff;
      }
    }

    loadPixels();
    for (int i=0; i<pixels.length; i++)
      pixels[i] = 0xff << 24 | 
        int(result[i][0]*1.0/samplesPerFrame) << 16 | 
        int(result[i][1]*1.0/samplesPerFrame) << 8 | 
        int(result[i][2]*1.0/samplesPerFrame);
    updatePixels();

    saveFrame("fr###.gif");
    println(frameCount,"/",numFrames);
    if (frameCount==numFrames)
      exit();
  }
}

//////////////////////////////////////////////////////////////////////////////

int samplesPerFrame = 4;
int numFrames = 75;        
float shutterAngle = 0.8;

boolean recording = true;

OpenSimplexNoise noise;

float xmin,xmax;

float ymin,ymax;

void setup(){
  size(600,600,P3D);
  result = new int[width*height][3];
  
  noise = new OpenSimplexNoise();
  
  xmin = -0.7*width;
  xmax = 0.7*width;
  
  ymin = -4.5*width;
  ymax = .8*width;
}

int n = 50;

int m = 2000;

int N = 6;

/*
float depth(float x,float y){
  return pow(max(0,(1-dist(x,y,0,0)/(f*width))),2.0)*10*(2*ease(map(sin(-PI+TWO_PI*(-2*t+7*pow((50+dist(x,y,0,0))/width,0.3))),-1,1,0,1),1.0)-1);
}
*/

float depth(float x,float y){
  float p = map(y,ymin,ymax,1,0);
  float q = map(x,xmin,xmax,0,1);
  return -300+250*(1-sin(PI*p))-150*pow(((cos(TWO_PI*q)+1)/2),2.0);
}

float f = 0.5;

void draw_(){
  background(0);
  push();
  translate(width/2,height/2-260,0);
  //rotate(HALF_PI);
  rotateX(0.28*PI);
  //rotate(+QUARTER_PI);
  
  /*
  float cameraZ = ((height/2.0) / tan(PI*60.0/360.0));
  perspective(PI/7.5, width/height, cameraZ/10.0, cameraZ*10.0);*/
  
  for(int i=0;i<n*N;i++){
    float c = (i%(2*N))<2?232:15;
    
    float x1 = map(i,0,n*N,xmin,xmax);
    float x2 = map(i+1,0,n*N,xmin,xmax);
    
    fill(c);
    noStroke();
    
    beginShape(TRIANGLE_STRIP);
    for(int j=0;j<m;j++){
      float y = map(j,0,m-1,ymin,ymax);
      
      float p = map(y,ymin,ymax,1,0);
      
      float xx1 = x1;
      float xx2 = x2;
      
      float offset = 8.0*pow(1-p,2.0)/5;
      
      float seed = 123;
      float rad = 2.0;
      float ds1 = 40*(float)noise.eval(seed+rad*cos(TWO_PI*(-t+offset)),rad*sin(TWO_PI*(-t+offset)),0.01*xx1);
      float ds2 = 40*(float)noise.eval(seed+rad*cos(TWO_PI*(-t+offset)),rad*sin(TWO_PI*(-t+offset)),0.01*xx2);
      float dy = 10*(float)noise.eval(2*seed+rad*cos(TWO_PI*(-t+offset)),rad*sin(TWO_PI*(-t+offset)),0.01*xx2);
      //float ds1 = 20*(float)noise.eval(0.02*y,0.01*x1);
      //float ds2 = 20*(float)noise.eval(0.02*y,0.01*x2);
      
      float d1 = 2*depth(xx1,y+dy)+dy;
      float d2 = 2*depth(xx2,y+dy)+dy;
     
      vertex(x1+ds1,y,d1);
      vertex(x2+ds2,y,d2);
    }
    endShape();
  }
  
  pop();
}
